apply plugin: 'com.android.application'

repositories {
    jcenter()
    google()
}

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.iamtechknow.terraview"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 11
        versionName "1.4"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resConfigs "en"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "app"

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            dimension "app"
        }
        prod {
            dimension "app"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    lintOptions { //disable lint
        tasks.lint.enabled = false
    }
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            //Force all of the primary support libraries to use the same version.
            if (details.requested.group == 'com.android.support')
                details.useVersion '27.1.1'
        }
    }
    exclude group: 'xpp3', module: 'xpp3'
    exclude group: 'stax', module: 'stax-api'
    exclude group: 'stax', module: 'stax'
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.15.0'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    api 'com.android.support:appcompat-v7:27.1.1'
    api 'com.android.support:support-v4:27.1.1'
    api 'com.android.support:design:27.1.1'
    api 'com.android.support:recyclerview-v7:27.1.1'
    api 'com.google.android.gms:play-services-maps:15.0.1'
    api 'com.getkeepsafe.taptargetview:taptargetview:1.11.0'

    api 'android.arch.lifecycle:extensions:1.1.1'
    api 'android.arch.lifecycle:common-java8:1.1.1'
    api 'android.arch.persistence.room:runtime:1.1.1'
    api 'android.arch.persistence.room:rxjava2:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'

    api 'com.google.code.gson:gson:2.8.2'
    api 'org.simpleframework:simple-xml:2.7.1'
    api 'com.squareup.okhttp3:okhttp:3.10.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    api 'com.squareup.retrofit2:retrofit:2.3.0'
    api 'com.squareup.retrofit2:converter-simplexml:2.3.0'
    api 'com.squareup.retrofit2:converter-gson:2.3.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    api 'io.reactivex.rxjava2:rxjava:2.1.14'
    api 'io.reactivex.rxjava2:rxandroid:2.0.2'
}
