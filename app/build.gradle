apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"

    defaultConfig {
        applicationId "com.iamtechknow.terraview"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 8
        versionName "1.2"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resConfigs "en"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "app"

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            dimension "app"
        }
        prod {
            dimension "app"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    lintOptions { //disable lint
        tasks.lint.enabled = false
    }
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            //Force all of the primary support libraries to use the same version.
            if (details.requested.group == 'com.android.support')
                details.useVersion '25.4.0'
        }
    }
    exclude group: 'xpp3', module: 'xpp3'
    exclude group: 'stax', module: 'stax-api'
    exclude group: 'stax', module: 'stax'
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    testApi 'junit:junit:4.12'
    testApi 'org.mockito:mockito-core:2.8.9'
    androidTestApi 'junit:junit:4.12'
    androidTestApi 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestApi 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestApi 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestApi 'com.android.support:support-annotations:25.4.0'
    androidTestApi 'com.android.support.test:runner:0.5'
    androidTestApi 'com.android.support.test:rules:0.5'

    api 'com.android.support:appcompat-v7:25.4.0'
    api 'com.android.support:support-v4:25.4.0'
    api 'com.android.support:design:25.4.0'
    api 'com.android.support:recyclerview-v7:25.4.0'
    api 'com.google.android.gms:play-services-maps:11.0.1'
    api 'com.google.code.gson:gson:2.8.1'
    api 'org.simpleframework:simple-xml:2.7.1'
    api 'com.getkeepsafe.taptargetview:taptargetview:1.9.1'

    api 'com.squareup.okhttp3:okhttp:3.8.1'
    api 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    api 'com.squareup.retrofit2:retrofit:2.3.0'
    api 'com.squareup.retrofit2:converter-simplexml:2.3.0'
    api 'com.squareup.retrofit2:converter-gson:2.3.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    api 'io.reactivex.rxjava2:rxjava:2.1.0'
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'
}
